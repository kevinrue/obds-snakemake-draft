import pandas as pd

###############
# User inputs #
###############

configfile: "config/config.yaml"

# read sample sheet
samples = (
    pd.read_csv(config["samplesheet"], sep="\t", dtype={"sample": str})
    .set_index("sample", drop=False)
    .sort_index()
)

# helper to build lists of fastqc outputs (one per existent read file)
fastqc_outputs = [
    re.sub(r'\.fastq\.gz$', '_fastqc.html', f)
    for f in list(samples["read1"]) + list(samples["read2"])
]

############
# Workflow #
############

rule all:
    input:
        "results/multiqc/fastqc_report.html",
        "results/multiqc/hisat2_report.html",
        "results/multiqc/featurecounts_report.html",

rule fastqc:
    input:
        fastq=lookup(query="sample == '{sample}'", within=samples, cols="{read}")
    output:
        html="results/fastqc/{sample}_{read}_fastqc.html",
        zip="results/fastqc/{sample}_{read}_fastqc.zip",
    threads: 2
    resources:
        mem_mb=2000,
        runtime=10,
        slurm_partition="short",
    shell:
        """
        fastqc --outdir results/fastqc -t {threads} {input.fastq}
        """

rule multiqc_fastqc:
    input:
        expand(
            "results/fastqc/{sample}_{read}_fastqc.{ext}",
            sample=samples.index,
            read=["read1", "read2"],
            ext=["html", "zip"],
        ),
    output:
        html="results/multiqc/fastqc_report.html",
    threads: 1
    resources:
        mem_mb=2000,
        runtime=10,
        slurm_partition="short",
    shell:
        """
        multiqc results/fastqc --outdir results/multiqc --filename fastqc_report.html
        """


rule hisat2:
    input:
        read1=lookup(query="sample == '{sample}'", within=samples, cols="read1"),
        read2=lookup(query="sample == '{sample}'", within=samples, cols="read2"),
    output:
        bam="results/hisat2/{sample}.sorted.bam",
    threads: 8
    resources:
        mem_mb=16000,
        runtime=60,
        slurm_partition="short",
    shell:
        """
        mkdir -p results/hisat2
        hisat2 -x /project/shared/linux/5_rnaseq/hisat2_index/grcm39 \
            -1 {input.read1} -2 {input.read2} -p {threads} \
            | samtools sort -@ {threads} -o {output.bam}
        """

rule samtools_index:
    input:
        bam="results/hisat2/{sample}.sorted.bam",
    output:
        bai="results/hisat2/{sample}.sorted.bam.bai",
    threads: 8
    resources:
        mem_mb=8000,
        runtime=10,
        slurm_partition="short",
    shell:
        """
        samtools index {input.bam}
        """

rule samtools_flagstat:
    input:
        bam="results/hisat2/{sample}.sorted.bam"
    output:
        txt="results/hisat2/{sample}.flagstat.txt"
    threads: 1
    resources:
        mem_mb=2000,
        runtime=10,
        slurm_partition="short",
    shell:
        """
        samtools flagstat {input.bam} > {output.txt}
        """

rule samtools_idxstats:
    input:
        bam="results/hisat2/{sample}.sorted.bam",
        bai="results/hisat2/{sample}.sorted.bam.bai",
    output:
        txt="results/hisat2/{sample}.idxstats.txt"
    threads: 1
    resources:
        mem_mb=2000,
        runtime=10,
        slurm_partition="short",
    shell:
        """
        samtools idxstats {input.bam} > {output.txt}
        """

rule multiqc_hisat2:
    input:
        expand(
            "results/hisat2/{sample}.{suffix}",
            sample=samples.index,
            suffix=[
                "sorted.bam",
                "sorted.bam.bai",
                "flagstat.txt",
                "idxstats.txt",
            ],
        ),
    output:
        html="results/multiqc/hisat2_report.html",
    threads: 1
    resources:
        mem_mb=2000,
        runtime=10,
        slurm_partition="short",
    shell:
        """
        multiqc results/hisat2 --outdir results/multiqc --filename hisat2_report.html
        """

rule featurecounts:
    input:
        expand(
            "results/hisat2/{sample}.sorted.bam",
            sample=samples.index,
        ),
    output:
        txt="results/featurecounts/counts.txt",
    threads: 8
    resources:
        mem_mb=8000,
        runtime=10,
        slurm_partition="short",
    shell:
        """
        mkdir -p results/featurecounts
        featureCounts \
            -T {threads} \
            -p \
            -a /project/shared/linux/5_rnaseq/genomes/gtf/Mus_musculus.GRCm39.113.gtf.gz \
            -o {output.txt} \
            {input}
        """

rule multiqc_featurecounts:
    input:
        "results/featurecounts/counts.txt",
    output:
        html="results/multiqc/featurecounts_report.html",
    threads: 1
    resources:
        mem_mb=2000,
        runtime=10,
        slurm_partition="short",
    shell:
        """
        multiqc results/featurecounts \
            --outdir results/multiqc \
            --filename featurecounts_report.html
        """
